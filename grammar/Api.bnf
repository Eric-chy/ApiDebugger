{
  classHeader="license.txt"
  generate=[java="8" names="long" visitor-value="R"]

  parserClass="io.chengguo.api.debugger.lang.parser.ApiParser"
  parserUtilClass="io.chengguo.api.debugger.lang.parser.ApiParserUtil"

  implements="io.chengguo.api.debugger.lang.psi.ApiElement"
  extends="io.chengguo.api.debugger.lang.psi.impl.ApiElementImpl"

  psiClassPrefix="Api"
  psiImplClassSuffix="Impl"
  psiPackage="io.chengguo.api.debugger.lang.psi"
  psiImplPackage="io.chengguo.api.debugger.lang.psi.impl"
  psiImplUtilClass="io.chengguo.api.debugger.lang.psi.impl.ApiPsiImplUtil"

  elementTypeHolderClass="io.chengguo.api.debugger.lang.psi.ApiTypes"
  elementTypePrefix="Api_"
  elementTypeClass="io.chengguo.api.debugger.lang.psi.ApiElementType"
  tokenTypeClass="io.chengguo.api.debugger.lang.psi.ApiTokenType"

  tokens=[
    COLON                   = ":"
    SLASH                   = "/"
    SCHEME_SEPARATOR        = "://"
    HTTP                    = "http"
    HTTPS                   = "https"
    QUESTION_MARK           = "?"
    EQUALS                  = "="
    AMPERSAND               = "&"
    LBRACES                 = "{{"
    RBRACES                 = "}}"
    HYPHEN                  = "-"
  ]
}

apiFile ::= api_*

private api_ ::= api_block (SEPARATOR api_block?)*

api_block ::= (description request) | LINE_COMMENT | MULTILINE_COMMENT

description ::= description_title description_content?
description_title ::= '-' DESCRIPTION_KEY ':' LINE_TEXT {pin=1}
description_content ::= '-' DESCRIPTION_KEY ':' LINE_TEXT {pin=1}

request ::= request_line header_field* message_body

request_line ::= METHOD request_target

request_target ::= scheme '://' host (':' port?)? path_absolute
scheme ::= 'http' | 'https'
host ::= (variable | HOST_VALUE)+
port ::= (variable | PORT_SEGMENT)+

path_absolute ::= path_segment* path_query?
private path_segment ::= '/' SEGMENT?
private path_query ::= '?' query

query ::= query_parameter ('&' query_parameter)*
query_parameter ::= query_parameter_key ('=' query_parameter_value)?
query_parameter_key ::= (variable | QUERY_NAME)+
query_parameter_value ::= (variable | QUERY_VALUE)+

header_field ::= header_field_key (':' header_field_val?)?
header_field_key ::= (variable | HEADER_FIELD_NAME)+
header_field_val ::= (variable | HEADER_FIELD_VALUE)+

private message_body ::= request_message_group /* | multipart_message*/
request_message_group ::= MESSAGE_TEXT

variable ::= '{{' variable_name? '}}'

variable_name ::= IDENTIFIER